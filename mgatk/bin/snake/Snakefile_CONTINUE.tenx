import os
import subprocess
import shutil
import pysam
from os.path import join

# Parse the configuration variables
configfile : config["cfp"]
outdir = config["output_directory"]
mgatk_directory = outdir
name = config["name"]
script_dir = config["script_dir"]          
indir = config["input_directory"]

mito_genome = config["mito_chr"]
mito_length = str(config["mito_length"])
fasta_file = config["fasta_file"]

remove_duplicates = config["remove_duplicates"]
barcode_tag = config["barcode_tag"]
umi_barcode = config["umi_barcode"]
emit_base_qualities = config["emit_base_qualities"]

handle_overlap = config["handle_overlap"]
proper_paired = config["proper_paired"]
base_qual = str(config["base_qual"])
alignment_quality = config["alignment_quality"]
NHmax = config["NHmax"]
NMmax = config["NMmax"]
low_coverage_threshold = config["low_coverage_threshold"]

max_javamem  = config["max_javamem"]

# Software paths
java = "java"
python = "python"

# Script locations
filtclip_py = script_dir + "/bin/python/filterClipBam.py"
detailedcall_py = script_dir + "/bin/python/detailedCalls.py"
sumstatsBP_py = script_dir + "/bin/python/sumstatsBP.py"
picardCall = java + " -Xmx"+max_javamem+" -jar " + script_dir + "/bin/picard.jar MarkDuplicates"

# A Snakemake regular expression matching the bam file paths
SAMPLES, = glob_wildcards(join(outdir, ".internal/samples/{sample}.bam.txt"))
bamtxtin = ''

rule all:
	input:
		mgatk_directory + "/final/" + name + ".depthTable.txt",
		mgatk_directory + "/final/" + name + ".A.txt.gz",
		mgatk_directory + "/final/" + name + ".C.txt.gz",
		mgatk_directory + "/final/" + name + ".G.txt.gz",
		mgatk_directory + "/final/" + name + ".T.txt.gz",	
		mgatk_directory + "/final/" + name + ".coverage.txt.gz",
		mgatk_directory + "/final/" + name + ".variant_stats.tsv.gz",
		mgatk_directory + "/final/" + name + ".cell_heteroplasmic_df.tsv.gz",
		mgatk_directory + "/final/" + name + ".vmr_strand_plot.png"

rule make_final_sparse_matrices:
	input:
		As = expand(mgatk_directory + "/temp/sparse_matrices/{sample}.A.txt", sample=SAMPLES),
		Cs = expand(mgatk_directory + "/temp/sparse_matrices/{sample}.C.txt", sample=SAMPLES),
		Gs = expand(mgatk_directory + "/temp/sparse_matrices/{sample}.G.txt", sample=SAMPLES),
		Ts = expand(mgatk_directory + "/temp/sparse_matrices/{sample}.T.txt", sample=SAMPLES),
		Covs = expand(mgatk_directory + "/temp/sparse_matrices/{sample}.coverage.txt", sample=SAMPLES)
		
	output:
		A = mgatk_directory + "/final/" + name + ".A.txt.gz",
		C = mgatk_directory + "/final/" + name + ".C.txt.gz",
		G = mgatk_directory + "/final/" + name + ".G.txt.gz",
		T = mgatk_directory + "/final/" + name + ".T.txt.gz",
		Cov = mgatk_directory + "/final/" + name + ".coverage.txt.gz"
		
	run: 
		def makeSM(iterableThing, l):
			for i in iterableThing:
				os.system("cat " + i + " >> " + mgatk_directory + "/final/" + name + "."+l+".txt")
			os.system("gzip " + mgatk_directory + "/final/" + name + "."+l+".txt")
			
		makeSM(input.As, "A")
		makeSM(input.Cs, "C")
		makeSM(input.Gs, "G")
		makeSM(input.Ts, "T")
		makeSM(input.Covs, "coverage")
		
rule call_variants:
	input:
		A = mgatk_directory + "/final/" + name + ".A.txt.gz",
		C = mgatk_directory + "/final/" + name + ".C.txt.gz",
		G = mgatk_directory + "/final/" + name + ".G.txt.gz",
		T = mgatk_directory + "/final/" + name + ".T.txt.gz",
		chrM_ref = mgatk_directory + "/final/" + mito_genome + "_refAllele.txt"

	output:
		variant_stats = mgatk_directory + "/final/" + name + ".variant_stats.tsv.gz",
		heteroplasmy_df = mgatk_directory + "/final/" + name + ".cell_heteroplasmic_df.tsv.gz",
		vmr_strand_plot = mgatk_directory + "/final/" + name + ".vmr_strand_plot.png"
		
	run:
		call_variant_py = script_dir + "/bin/python/variant_calling.py"
		pycall = " ".join(['python', call_variant_py, mgatk_directory + "/final/", name, mito_length, low_coverage_threshold, mito_genome])
		os.system(pycall)
